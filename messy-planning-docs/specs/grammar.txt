PROGRAM = (VARIABLE_ASSIGN | VARIABLE_SET_CONSTANT | EXPRESSIONABLE | OBJECT_STATEMENT | TRAIT_STATEMENT)*
EXPRESSIONABLE = (VARIABLE_GET | LITERAL | KEYWORD COLON? | FUNCTION_CALL | MATCH_STATEMENT | FUSION | FUSE_MODIFIER)
FUSE_MODIFIER = "~" EXPRESSIONABLE EXPRESSIONABLE
VARIABLE_ASSIGN = "=" (".")? NAME (COLON TYPE)?
VARIABLE_SET_CONSTANT = "=!" NAME
VARIABLE_GET = "$" (".")? NAME
LITERAL = NUMBER | STRING | LIST | FUNCTION
NUMBER = DECIMAL ("i" DECIMAL)?
DECIMAL = DIGITS ("." [0-9]+)? "_"?
DIGITS = 0 | [1-9] [0-9]*
STRING = '"' ([^"] | '\\' .)* '"'
LIST = "[" (EXPRESSIONABLE ("," EXPRESSIONABLE)*)? "]"
KEYWORD = ([A-Za-z+/*^%!-<>][A-Za-z0-9_?=!-]*) | "==" | "==="
NAME = [A-Za-z_][A-Za-z0-9_]*
FUNCTION_CALL = "`" NAME "`"
MATCH_STATEMENT = "#match:" "{" EXPRESSIONABLE? "=>" PROGRAM ("|" EXPRESSIONABLE? "=>" PROGRAM)* "}"
OBJECT_STATEMENT = "#object:" NAME GENERICS? ("implements" "[" TYPE ("," TYPE)* "]") ":" FUNCTION
TRAIT_STATEMENT = "#trait:" NAME GENERICS? ("implements" "[" TYPE ("," TYPE)* "]") ":" FUNCTION
FUNCTION = "{" ("(" GENERICS? FUNCTION_ARGS? ")" "=>")? PROGRAM? "}"
FUNCTION_ARGS = FUNCTION_ARG ("," FUNCTION_ARG)* ("," "$:" TYPE)?
FUNCTION_ARG = (NAME (COLON TYPE)?) | COLON TYPE | DIGITS | FUSION_TYPE
FUSION = "@(" EXPRESSIONABLE ("," EXPRESSIONABLE)* ")"
FUSION_TYPE = "@(" ( TYPE | NAME (COLON TYPE)?)* ")"
GENERICS = "[" TYPE ("," TYPE)* "]"
TYPE = NAME GENERICS? ("+"+ | "~"+ | "!")? "?"?

